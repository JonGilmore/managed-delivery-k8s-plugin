buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id("io.spinnaker.plugin.bundler").version("$spinnakerGradleVersion")
    id("com.palantir.git-version").version("0.12.2")
    id("org.jetbrains.kotlin.jvm").version("$kotlinVersion")
    id("org.jetbrains.kotlin.kapt").version("$kotlinVersion")
    id("com.diffplug.spotless").version("5.1.0")
}

allprojects {
    repositories {
        jcenter() {
            metadataSources {
                artifact()
                mavenPom()
            }
        }
        mavenCentral()
        if (property("korkVersion").toString().endsWith("-SNAPSHOT")) {
            mavenLocal()
        }
    }
}

spinnakerBundle {
    pluginId = "aws.ManagedDeliveryK8sPlugin"
    description = "K8s support for Managed Delivery"
    provider = "https://aws.amazon.com"
    version = rootProject.version
}

version = normalizedVersion()

subprojects {
    group = "com.amazon.spinnaker.keel.k8s"
    version = rootProject.version
}

String normalizedVersion() {
    String fullVersion = gitVersion()
    String normalized = fullVersion.split("-").first()
    if (fullVersion.contains("dirty")) {
        return "$normalized-SNAPSHOT"
    } else {
        return normalized
    }
}